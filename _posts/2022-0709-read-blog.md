---
layout: post
title: LLisp: Lisp in Lisp
date:   2022-07-09
categories: 勝手翻訳 Clojure
---


# LLisp: Lisp in Lisp



先週、私はあることを考えた。"私が書ける最も簡単なLispインタプリタで、マクロをサポートしているものは何か？"

週末にClojureをハックし、いくつかの混乱を経て、REPLが誕生しました。 このエッセイでは、同じ旅を経て、Clojureでマクロをサポートする独自のLispインタプリタを作ってみましょう! LLisp: a lisp in a lispとでも呼びましょうか。

最後に、あなたは自分のマクロを自分のプログラミング言語で書くことができます。さあ、始めましょう。

# スケッチ

さて、これから行うべきことの基本的な考え方をスケッチしてみましょう。


![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc1NC1lMTg0NmZlNC0yM2I3LTQ3ZTctYjJmNy1lMjJiMDJlZmU2NmQucG5n){style="max-width:100%"}


プログラマーが何かコードを書く。ここまでは単なるテキストで、私たちができることはあまりない。私たちはプログラマーの書いたテキストを読み、それをデータ構造に変換する。データ構造を評価し、プログラマが意図したものを返すことができるのだ。

もし、`"(+ 1 1)"` を `2` に変換することができれば、私たちはプログラミング言語のルーツを手に入れることができるのです。

# Reader

2.読み」を処理してみよう。プログラマが `"(+ 1 1)"` のようなテキストを書いたら、それをデータ構造に変換してあげたい。みたいな感じ。

```
(read-string "(+ 1 1)")
; =>
(+ 1 1)
```

自分たちで `read-string` を書くことは *可能* です。最も単純なケースでは、それは非常に簡単です^\[1\]^。しかし、我々は結局Clojureにいる。そして、ClojureはすでにLispコードの読み方を理解している。ズルしてClojureの `edn` を使おう。

```
(ns simple-lisp.core
  (:refer-clojure :exclude [eval read read-string])
  (:require [clojure.edn :refer [read read-string]]))
```

そして、ほら、`read-string` は私たちが望むことをします。

```
(read-string "(+ 1 1)")
; =>
(+ 1 1)
```

# Evaluation

さて、次は「3.評価」です。我々のゴールはデータ構造を受け取り、それを評価し、プログラマーが意図したものを返すことだ。まずは、こんな感じの簡単な関数から始めてみましょう。

```
(defn eval [form] 
  ;; TODO 
  )
```

## リテラルを評価する

簡単なことから始めましょう。例えば、プログラマが `12` と書いたら、 `12` という数字を返すだけでよいのです。 これは文字列(`"foo"`)、文字(`\b`)、ブーリアン(`true`)についても同じことが言えます。 これらはリテラル*.*です。

ここで、リテラルを評価する方法を説明します。まず、それらを検出しましょう。

```
(def literal?
  (some-fn string? number? boolean? char? nil?))
```

もしあなたが `some-fn` を見たことがなければ、これは clojure の数ある `便利で強力な` ユーティリティのうちの一つです。これで `eval` でリテラルを扱えるようになりました。

```
(defn eval [form]
  (cond
    (literal? form) form))
```

そして、私たちのプログラミング言語が何かをし始めるのです。

```
(eval (read-string "12"))
; => 12
```

## 引用の紹介

それでは、`quote`について説明します。 quoteはLispでは非常に重要な形式です。これにより、評価されないコードを書くことができます。例えば

```
(quote (+ 1 1))
; => 
(+ 1 1)
```

`(+ 1 1)` は `quote` の中にあるので、それを評価して `2` を返すことはしませんでした。 その代わり、式そのものを返します。これは引用符をサポートしない言語から来た人には奇妙に思えるかもしれませんが、Lispでは常に使用されている方法です。実際、そのためのショートカットがあります。

```
'(+ 1 1) 
```

これを読み込むと、次のように変換されます。

```
(quote (+ 1 1))
```

ありがたいことに、edn の `read-string` がこれを代行してくれます。 🙂

## クォートの評価

では、どのようにしてクォートを評価するのでしょうか。まず、クォートを検出する関数を作ってみましょう。

```
(defn seq-starts-with? [starts-with form]
  (and (seqable? form) (= (first form) starts-with)))

(def quote? (partial seq-starts-with? 'quote))
```

フォームがリストで、最初の要素がシンボル `quote` であるかどうかを確認します。

```
(quote? (read-string "'(+ 1 1)"))
; => 
true
```

魅力的なように動作します。では、`eval`を更新してみましょう。

```
(defn eval [form]
  (cond
    (literal? form) form
    (quote? form) (second form)))
```

フォームが `quote` であれば、その「second」部分を返します。そして出来上がり。

```
(eval (read-string "(quote (+ 1 1))"))
; => (+ 1 1)
```

## シンボルの紹介

次にシンボルについて説明します。シンボルはLispの特殊なデータ型です。変数と同じで、シンボルを評価するときに、それを調べるという意味です。



![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc2MS01YmQ2ODhlOS00ZDEzLTQ3ZjMtYTg4My1lYTZhMTdlYWRjNjEucG5n){style="max-width:100%"}


どこで調べるんだ？インタープリタにはある種の `environment` が必要で、それは私たちが定義したすべての変数を追跡します。例えば、`fav-num` というシンボルが `41` を指しているような環境があった場合、`fav-num` を評価すると次のようになります。

```
(eval env (read-string "fav-num"))
; => 
41
```

## シンボルを評価する

まず、この `environment` を作成しましょう。 シンボルとその値の対応関係を保持するために、java の `Hashmap` を利用することができる。

java の Hashmap をインポートしよう。

```
(ns simple-lisp.core
  (:refer-clojure :exclude [eval read read-string])
  (:require [clojure.edn :refer [read read-string]])
  (:import (java.util HashMap)))
```

そして、`env` マップを作成するための簡単な関数を作成します。

```
(defn env [] {:globe (HashMap. {'fav-num 41})})
```

これで `eval` で `env` マップを受け取り、シンボルを扱うことができるようになりました。

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)))
```

Clojureにはすでに便利な `symbol?` という関数があります。それがtrueなら、 `lookup-symbol` とします。 以下は `lookup-symbol` がどのように見えるかです。

```
(defn lookup-symbol [{:keys [globe]} sym]
  (let [v (when (contains? globe sym) [(get globe sym)])]
    (assert v (format "expected value for sym = %s" sym))
    (first v)))
```

そして、シンボルが機能する!

```
(eval (env) (read-string "fav-num"))
; => 
41
```

## プリミティブの評価

次は、`(+ 1 1)` を動作させるための重要なステップです。私たちは `+` が何かを意味するようにしたいと思うでしょう。シンボル `+` は何を指すべきでしょうか？

Clojureの関数はどうでしょうか？

```
(defn env [] {:globe (HashMap. {'+ + 'fav-num 41})})
```

そうすると、今度はシンボル `+` を評価するたびに clojure の関数が返されるようになります。

```
(eval (env) (read-string "+"))
; => #object[clojure.core$_PLUS_ 0x4efcb4b5 "[email protected]"]
```

また、evalでclojureの関数を「受け取った」場合、それをリテラルとして扱えばいいだけです。

```
(def literal?
  (some-fn string? number? boolean? char? nil? fn?))
```

```
(eval (env) +)
; => #object[clojure.core$_PLUS_ 0x4efcb4b5 "[email protected]"]
```

これは私たちのエッセイでは小さな一歩ですが、私たちのLispでは大きな一歩です。 これで `(+ 1 1)` のすべての要素が評価できるようになりました。

## 原始的なアプリケーション

では、これらの関数をどのように実行すればよいのでしょうか？では、 `eval` を更新して、 `(+ 1 1)` のようなリストを扱えるようにしましょう。

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)
    :else (eval-application env form)))
```

簡単だ。もし `form` が何であるか理解できなければ、それはリストであり、それを実行したいことを意味するに違いありません! これは `eval-application` がどのように動作するかを示しています。

```
(defn eval-application [env [f & args]]
  (let [f-evaled (eval env f)]
    (cond
      (fn? f-evaled) (apply f-evaled (eval-many env args)))))
```

`eval-application` では、リストの *最初の* 部分を評価します。この例では、シンボル `+` が評価され、clojure関数が返されます。

もし `f-evaled` が clojure の関数であれば(実際そうです)、次のように実行されます。

```
(apply f-evaled (eval-many env args))
```

`eval-many` は、フォームのリストを評価するための単なるヘルパーです。

```
(defn eval-many [e forms] (map (partial eval e) forms))
```

`args` は `(1 1)` となるので、 `eval-many` は `(1 1)` を返すことになります。

```
(apply + '(1 1))
```

すると、`2`が返され、`(+ 1 1)`が動作するようになります!

```
(eval (env) (read-string "(+ 1 1)")
; => 
2
```

再帰的に評価するので、すでに複雑なことができることを忘れないでください。

```
(eval (env) (read-string "(+ 1 (+ 1 fav-num))")
; => 
43
```

## def の紹介

OKY DOKE, 我々は環境を手に入れ、その環境から何かを探し出すシンボルを手に入れ、さらに `(+ 1 (+ 1 fav-num))` を評価することができるようになりました。

しかし、どうやって変数を定義するのでしょうか？例えば、`second-fav`というシンボルが`42`を指すようにしたいとしましょう。

そうすると、特殊な形式を導入することができます。このようなものです。

```
(def second-fav (+ 1 fav-num))
```

この `def` フォームを受け取ったら、環境を更新して、シンボル `second-fav` を `(+ 1 fav-num)` の評価値がどうなるかに向けるだけでいいのです。

## Defを評価する

という感じですね。まずはこのフォームを検出してみましょう。

```
(def def? (partial seq-starts-with? 'def))
```

そして、それを処理するために `eval` を更新します。

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)
    (def? form) (eval-def env form)
    :else (eval-application env form)))
```

そして、`eval-def`が必要とすることは、これだけです。

```
(defn eval-def [env [_ k v]]
  (assert (symbol? k) (format "expected k = %s to be a symbol" k))
  (.put (:globe env) k (eval env v)))
```

ここでは、`def` の最初の引数がシンボル `k` であることが分かっています。 2 番目の引数は保存したい *値* `v` です。そこで、環境の `globe` ハッシュマップを更新して、シンボル `k` を `(eval env v)` に指定します。この場合、 `k` は `second-fav` で、 `v` は `(+ 1 fav-num)` となり、 `(eval env v)` は `42` となります。

かっこいいですね、これは。

```
(let [e (env)] 
    (eval e (read-string "(def second-fav (+ 1 fav-num))"))
    (eval e (read-string "second-fav")))
; => 
42
```

## ifの導入

さて、もう一つ特殊な形式を実装してみましょう。条件付きロジックを処理するものが必要だ。

```
(if (= fav-num 41) 'yess 'noo))
```

`if` を得ると、テストフォーム`(= fav-num 41)`を評価することができます。もしそれが真なら、真のケース `'yess` を評価します。 そうでなければ、偽のケースを評価することになります。`'noo` となります。 計画的ですね。

## ifの評価

ifを実装するために、いつものようにまず検出しましょう。

```
(def if? (partial seq-starts-with? 'if))
```

そして、それを `eval` で使用します。

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)
    (def? form) (eval-def env form)
    (if? form) (eval-if env form)
    :else (eval-application env form)))
```

そして、`eval-if`はこんな感じでしょうか。

```
(defn eval-if [env [_ test-form when-true when-false]]
  (let [evaled-test (eval env test-form)]
    (eval env (if evaled-test when-true when-false))))
```

ほんの数行、私たちの説明に忠実に従ったものです。`evaled-test`が真であるかどうかを確認します。そして、 `when-true` か `when-false` のどちらかを評価する。

ついでに、 `env` に素敵なプリミティブ関数をたくさん追加してみよう。

```
(defn env [] {:globe (HashMap. {'+ + '= = 'list list 'map map 'concat concat
                                'first first 'second second 'not not 'fav-num 41})})
```

そして、バーン。この小さなインタプリタがかなり多くのことをできるようになったのです

```
    (eval (env) (read-string "(if (= fav-num 41) 'yess 'noo)"))
    ; => yess
```

## 関数の紹介

さらに上を目指しましょう。Clojureの `+` などはありますが、プログラマが *自分自身の* 関数を書きたいとしたらどうでしょうか？ここに例があります。

```
((clo (x) (+ x fav-num) 2))
```

ここで、プログラマは `x` を受け取り、それに `fav-num` を加える関数を書いたとします。

彼らがこう書いたとします。彼らの関数を実行するにはどうしたらいいでしょうか？さて、彼らの定義には `arguments` と `body` があります。 この場合、それぞれ `(x)` と `(+ x fav-num)` です。この例では、関数は `2` で呼び出されます。

もし、 `(+ x fav-num)` を評価することができれば、 `x` が `2` を指す環境で、まるで関数を *ran* したかのようになるのです! このアイデアを説明するための図があります。


![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc2OC00MzBkN2ZjMC05MDQyLTQ0ZGUtYmYyOS0zNjM4ZjA5YTllYzkucG5n){style="max-width:100%"}


どうやって `x` に `2` を指定するのでしょうか？ なぜなら、`body` の評価が終わった後でも、`x` は *永久に* `2` に設定されてしまうからです。

だから、新しいアイディアが必要なんだ。スコープ`です。 スコープ`は環境として考えることができ、そこでは `body` が評価されている間だけ変更が持続する。このような概念を導入することができれば、関数ができることになる。

## 関数の評価

さて、これを動作させましょう。まず、誰かが `(clo (x) (+ x 1))` と書いたら、実際には `eval-application` をしようとしないことを確認しましょう。 その代わり、これを新しい `closure` リテラルとして扱います。

それを検出することができます。

```
(def closure? (partial seq-starts-with? 'clo))
```

そして、私たちの `literal?` を更新してください。

```
(def literal?
  (some-fn string? number? boolean? char? nil? fn? closure?))
```

今、ユーザーが関数を書いただけでは、それ自体を返してしまいます。

```
(eval (env) (read-string "(clo (x) (+ x fav-num))"))
; => (clo (x) (+ x fav-num))
```

次に、`環境`を更新して、新しい`scope`マップを持つようにします。

```
(defn env [] {:globe (HashMap. {'+ + '= = 'list list 'map map 'concat concat
                                'first first 'second second 'fav-num 41})
              :scope {}})
```

そして、`lookup-symbol` を更新して、スコープ内の変数 *も* 探すようにしましょう。

```
(defn lookup-symbol [{:keys [globe scope]} sym]
  (let [v (some (fn [m] (when (contains? m sym) [(get m sym)]))
                [globe scope])]
    (assert v (format "expected value for sym = %s" sym))
    (first v)))
```

もっと近くに、もっと近くに。さあ、実行させましょう。こう書けば

```
((clo (x) (+ x fav-num)) 1)
```

このリストは `eval-application` に行くことになるでしょう。 更新してみましょう。

```
(defn eval-application [env [f & args]]
  (let [f-evaled (eval env f)]
    (cond
      (fn? f-evaled) (apply f-evaled (eval-many env args))
      (closure? f-evaled) (eval-closure env f-evaled (eval-many env args)))))
```

`f-evaled` は `(clo (x) (+ x fav-num))` となります。 これは `(closure? f-evaled)` が真になることを意味します。

`(eval-many env args)`ですべての引数を評価すると `(1)` となり、これを `eval-closure` に渡します。 以下は `eval-closure` がどのように見えるかです。

```
(defn eval-closure [env [_ syms body] args]
  (eval (assoc env :scope (assign-vars syms args)) body))
```

図と同じようにします。そして `syms` は `(x)` となり、`args` は `(1)` となります。 そして、新しいスコープを作成します。

```
(defn assign-vars [syms args]
  (assert (= (count syms) (count args))
          (format "syms and args must match syms: %s args: %s"
                  (vec syms) (vec args)))
  (into {} (map vector syms args)))
```

これは `{x 2}` です。 これを環境にマージして、`body`を評価します。

突然ですが、`(+ x fav-num)` は `43` になります!

```
(eval (env) (read-string "((clo (x) (+ x fav-num)) 2)"))
; => 43
```

## レキシカル・スコープを導入する

多くの言語がレキシカル・スコープをサポートしている。例えば、javascriptでは以下のようなことができます。

```
const makeAdder = (n) => (a) => n + a
const addTwo = makeAdder(2);
addTwo(4) 
// => 
6
```

ここで、 `makeAdder` は関数を返します。この関数は `n` の値を *定義されている場所* で記憶していました。これを私たちの言語で実現するにはどうしたらいいのでしょうか？

さて、まず最初に、プログラマに現在のスコープを見せるようにするとしよう。以下はその例です。

```
((clo (x) scope) 2)
; => 
{x 2}
```

この特別な `scope` シンボルは、評価されると `{x 2}` を返し、 *current* スコープになります!

さて、関数リテラルを更新してみるとどうでしょう？次のような感じでしょうか。

```
(clo {n 2} (a) (+ n a))
```

ここでは、`scope`のための特別な場所があります!  関数を定義するときに、その関数が定義されている場所のスコープをそこに「ポン」と置くことができれば、badabing badaboom, lexical scoping をサポートすることになります!

ここで、 `make-adder` がどのように動作するのか見てみましょう。

```
(def make-adder (clo nil (n) 
                  (list 'clo scope '(y) '(+ y n)))) 
(def add-two (make-adder 2))
(add-two 4)
; => 
6
```

`(make-adder 2)`を実行すると、`(list 'clo scope '(y) '(+ y n))` を評価することになります。 これは `(clo {n 2} (y) (+ y n))` を返すでしょう!  突然ですが、私たちはレキシカルスコープをサポートしています。

`(list 'clo scope '(y) '(+ y n)))` --- と書くのは変だと思うかもしれません。でもね、もうすぐマクロがサポートされるから、そのおかげでこの書き方がぴったりになるんだよ。待っててね 🙂.

## レキシカルスコープを実装する

さて、説明が多くなってしまいましたが、変更は簡単です。まず、`scope` シンボルを取得したら、現在のスコープを返すようにする必要があります。

```
(defn lookup-symbol [{:keys [globe scope]} sym]
  (let [v (some (fn [m] (when (contains? m sym) [(get m sym)]))
                [{'scope scope} globe scope])]
    (assert v (format "expected value for sym = %s" sym))
    (first v)))
```

そして、あとは `eval-closure` を更新するだけです。

```
(defn eval-closure [env [_ scope syms body] args]
  (eval (assoc env :scope (merge scope (assign-vars syms args))) body))
```

ここでは、クロージャ自身から `scope` を取っています! これで、レキシカル・スコープは機能するようになりました。

```
(((clo nil (x)
        (list 'clo scope '(y) '(+ y x))) 2) 4)
; => 
6
```

## マクロの紹介

さて、これでマクロを実装するのに必要なものが揃いました。では、マクロとは何なのか考えてみましょう。

関数 `unless` を書くことを想像してください。

```
(def unless (clo nil (test v)
              (if (not test) v)))
```

うまくいくでしょうか？もし `(unless (= fav-num 41) 'yes)` を実行したら、`yes` という記号を返すでしょう。

しかし、これを実行したらどうでしょう？

```
(unless (= fav-num 41) (throw-error))
```

`unless` は関数なので、各引数を最初に評価しますよね？その場合、 `(= fav-num 41)` を評価することになり、これは `true` となります。 しかし、`(throw-error)` *も* 評価することになります。 これはプログラムを壊してしまいます。これは、 `(throw-error)` が `test` が偽のとき *だけ* 実行されることになっているため、 `unless` のすべての目的を破っています。



![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc3Mi1hNTlmNGJkOC1mNDc1LTQxMGMtODFmYi02YWI2NjlhNDY1OTEucG5n){style="max-width:100%"}


ここで、*macro* unlessを書いたとします。

```
(def unless (mac (clo nil (test v)
                    (list 'if (list 'not test) v))))
```

もし `(unless (= fav-num 41) (throw-error))` を実行したら、次のようになります。

`test` の値は `true` ではなく、実際には `(= fav-num 41)` というリストになっているはずです。 同様に、 `(throw-error)` も評価しない。 v` は実際のリスト `(throw-error)` となります。

*マクロの引数は評価されません。 * マクロが実行されると、* コード*が返されます。

```
(list 'if (list 'not test) v))
```

これは、

```
(if (not (= fav-num 41)) (throw-error))
```
になります。

そして、*それ*を評価すると、期待通りに動作するのです!  `(throw-error)` は決して評価されません。

ここが関数とマクロの大きな違いです。関数は引数として*評価された値*を受け取り、評価された値を返します。マクロは *評価されない* 引数を受け取り、 *コードを返し、そのコードは評価されます*。


![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc3OS1mMDMxNWQyNS1lZjZlLTQzYjktOTBiNy1jMmYyMDE2YTkyOGIucG5n){style="max-width:100%"}


マクロに未評価の引数を与えると、新しいコードが返され、それをもう一度*評価*します。

では、どのようにこれをサポートすればよいのでしょうか。

## マクロの評価

少ないコードで多くの説明を。準備はいいですか？マクロを追加してみましょう。

マクロは `mac` で始まり、その中にクロージャを持つリストです。

```
(mac (clo nil (...))
```

マクロを検出し、それをリテラルとしてマークすることができます。

```
(def macro? (partial seq-starts-with? 'mac))
    
(def literal?
  (some-fn string? number? boolean? char? nil? fn? closure? macro?))
```

次に、`eval-application` を更新します。

```
(defn eval-application [env [f & args]]
  (let [f-evaled (eval env f)]
    (cond
      (fn? f-evaled) (apply f-evaled (eval-many env args))
      (closure? f-evaled) (eval-closure env f-evaled (eval-many env args))
      (macro? f-evaled) (eval-macro env f-evaled args))))
```

以前は常に `(eval-many env args)` を実行していました。 しかし今回は、マクロであれば `args` を直接渡すのです! これが「コード」そのものです🙂。

そして次は `eval-macro` です。

```
(defn eval-macro [env [_ clo] args]
  (eval env
        (eval env (concat [clo] (map (partial list 'quote) args)))))
```

なんということでしょう。3行！！ 図に書いたとおりのことをやっています。



![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc5MC02MWFhOTQ5OC05NDgzLTQ4ODYtOGRiMC1lYmM3NzE1OTM2MzkucG5n){style="max-width:100%"}


マクロから "クロージャ "を取り出して、*評価されない*引数で*実行*するのです。これは、各`arg`を quote で囲むことで行えます。`(map (partial list 'quote) args)`.

結果のコードができたら、それをまた *評価* して、マクロができあがります。

```
(def unless (mac (clo nil (test v)
                    (list 'if (list 'not test) v))))
; works!
(unless (= fav-num 41) (throw-error))
```

# 自分だけのマクロ

さて、マクロをサポートするLispができました。さっそく、自分なりのマクロを書いてみましょう。

## defmacro

さっそくメタな話をしましょう。マクロを定義するときに何をするかに注目してください。

```
(def unless (mac (clo nil (test v)
                      (list 'if (list 'not test) v))))
```

この `(mac (clo nil ...))` という作業は、ちょっと不要ですね。これをやってくれるマクロを書けばいいのです。

```
(def defmacro
      (mac (clo nil (n p e)
                (list 'def n
                      (list 'mac (list 'clo nil p e))))))
```

これは、`(def n (mac (clo nil ...)))` というコードを生成します。 これで、書けるようになりました。

```
(defmacro unless (test v) 
  (list 'if (list 'not test) v))
```

Cool!

## fn

さて、レキシカル・スコーピングのための関数を書いたのを覚えていますか？

```
(def make-adder (clo nil (n)
                      (list 'clo scope '(y) '(+ y n))))
```

これをマクロで書いてもらおう。

```
(defmacro fn (args body)
              (list 'list ''clo 'scope
                    (list 'quote args)
                    (list 'quote body)))
```

と書くと、こうなる。

```
(def make-adder (clo nil (n)
                    (fn (y) (+ y n))))

(def add-two (make-adder 2))
```

`fn` のマクロ展開が実行されると、`args` は `(y)` となり、`body` は `(+ y n)` となります。 つまり

```
(list 'list ''clo 'scope
      (list 'quote args)
      (list 'quote body))
```

に展開することになる。

```
(list 'clo scope '(y) '(+ y n))
```

そしてそれは、私たちが手で書いたものと *正確に* 一致しているのです! これで `'clo scope` の代わりに `fn` が使えるようになりました。

## defn

さて、関数を定義しようと思えば、こう書くことができる。

```
(def add-fav-num (fn (x) (+ x fav-num)))
```

しかし、もっとタイトにすることができます。`defn` マクロを追加して、書けるようにしましょう。

```
(defn add-fav-num (x) (+ x fav-num))
```

楽勝です。

```
(defmacro defn (n args body)
  (list 'def n (list 'fn args body)))
```

## let

もう一つ、クールなマクロを紹介します。今のところ、一時的な変数を定義する本当の方法はないんだ。こんな感じ。

```
(let ((x 1) (y 2)) 
  (+ x y))
; => 
3
```

どうすればサポートできるのでしょうか？では、`(let ...)` を *書き直し*たらどうでしょう。

```
((fn (x y) (+ x y)) 1 2)
```

関数の引数リストを使って、これらの変数を作ればいいんだ! 完璧だ。これがそのマクロです。

```
(defmacro let (pairs body)
  (concat (list (list 'fn (map first pairs) body))
                (map second pairs)))
```

# Fin

なんて旅なんだろう。80行、4000語、そしてできれば楽しい時間を過ごしたい🙂。あなたは今、マクロを使ったLispを手に入れ、いくつかのクールなマクロを自分で書いたのです。 ここから先は[フルソース](https://github.com/stopachka/llisp/blob/main/src/llisp/core.clj)なので、そこから追いかけたい人はどうぞ。

**クレジット**

マクロとクロージャをこのように表現するアイデアは、PGの[Bel](http://www.paulgraham.com/bel.html)から来たものです。

*このエッセイのドラフトをレビューしてくれたMark ShlickとDaniel Woelfelに感謝します。


------------------------------------------------------------------------

#### *Thoughts? Reach out to me via twitter or email : )*







