---
layout: post
title: Nim tutorial (II)
date:   2021-12-19
categories: 勝手翻訳 nim
---



# Nim Tutorial (Part II)

Author:   Andreas Rumpf

## はじめに

> "繰り返しは、ばかばかしいことを合理的にする" -- Norman Wildberger

このドキュメントは、プログラミング言語 *Nim* の高度な構成についてのチュートリアルです。**[マニュアル](manual.html)には、より高度な言語機能の例が多数掲載されていますので、この文書は多少古くなっています。**

## プラグマ

プラグマとは、大量の新しいキーワードを導入することなく、コンパイラに追加の情報やコマンドを与えるための Nim の方法です。プラグマは `{.` と `.}` という特殊な中括弧で囲まれます。このチュートリアルでは、プラグマは扱いません。利用可能なプラグマについては [manual](manual.html#pragmas) または [user guide](nimc.html#additional-features) を参照してください。

## オブジェクト指向プログラミング

Nim のオブジェクト指向プログラミング（OOP）に対するサポートは最小限ですが、強力な OOP テクニックを使用することが可能です。OOP はプログラムを設計するための *一つの* 方法であり、*唯一の* 方法ではありません。多くの場合、手続き的なアプローチは、よりシンプルで効率的なコードにつながります。特に、継承よりも合成を優先させた方が良い設計になることが多いです。

### 継承

Nimでの継承は完全に任意です。実行時の型情報を持つ継承を行うには、オブジェクトは `RootObj` から継承する必要があります。これは直接的に行うこともできるし、 `RootObj` を継承するオブジェクトを継承することで間接的に行うこともできる。通常、継承された型は `ref` 型としてマークされますが、これは厳密には強制されません。オブジェクトが特定の型であるかどうかを実行時に確認するために、 `of` 演算子を使用することができます。

```Nim
type
  Person = ref object of RootObj
    name*: string  # は `name` が他のモジュールからアクセス可能であることを意味します。
    age: int       # *がないのは、他のモジュールからフィールドが隠されることを意味します。
  
  Student = ref object of Person # Student は Person を継承しています。
    id: int                      # idフィールドを持つ

var
  student: Student
  person: Person
assert(student of Student) # is true
# オブジェクトの構築。
student = Student(name: "Anton", age: 5, id: 2)
echo student[]
```

継承は `object of` 構文で行われます。多重継承は現在のところサポートされていない。オブジェクトタイプに適切な祖先がいない場合、 `RootObj` を祖先として使用することができるが、これは単なる慣習に過ぎない。先祖を持たないオブジェクトは暗黙のうちに `final` となります。`system.RootObj` とは別に、新しいオブジェクトルートを導入するために、 `inheritable` プラグマを使用することができます。(これは例えば GTK ラッパーで使用されています)。

Refオブジェクトは、継承が行われるときには常に使用されるべきです。これは厳密には必要ではありませんが、非Refオブジェクトでは `let person.Object` のような代入を行うことができます。`Person = Student(id: 123)` のような代入はサブクラスのフィールドを切り詰めます。

**注**: 単純なコードの再利用のためには、継承(*is-a* relation)よりも合成(*has-a* relation)の方が望ましい場合があります。Nim ではオブジェクトは値型なので、合成は継承と同じように効率的です。

### 相互再帰的な型

オブジェクト、タプル、および参照は、互いに依存し合う非常に複雑なデータ構造をモデル化することができます; これらは *相互に再帰的* です。Nim ではこれらの型は1つの型セクションの中でしか宣言できません。(それ以外の場合、任意のシンボルルルックアヘッドが必要となり、コンパイルが遅くなります)。

例

```Nim
type
  Node = ref object  # 以下のフィールドを持つオブジェクトへの参照。
    le, ri: Node     # leftとrightのサブツリー
    sym: ref Sym     # 葉には、Symの参照が含まれています。
  
  Sym = object       # シンボル
    name: string     # シンボル名
    line: int        # シンボルが宣言された行
    code: Node       # シンボルの抽象構文木
```

### 型変換

Nim は型キャストと型変換を区別しています。キャストは `cast` 演算子を用いて行われ、コンパイラにビットパターンを別の型に解釈するように強制します。

型変換はある型を別の型に変換するための、より丁寧な方法である。型変換は抽象的な *値* を保持しますが、必ずしも *ビットパターン* を保持するわけではありません。型変換ができない場合、コンパイラは文句を言うか、例外を発生させる。

型変換のシンタックスは `destination_type(expression_to_convert)` です（普通の呼び出しと同じです）。

``` nim
proc getID(x: Person): int =
  Student(x).id
```

`x` が `Student` でない場合、 `InvalidObjectConversionDefect` 例外が発生します。

### オブジェクトのバリアント

単純なバリアントタイプが必要な状況では、オブジェクト階層が過剰になることがよくあります。

その例です。

```Nim
# これは、Nimで抽象的な構文木をどのようにモデル化できるかの例です。
type
  NodeKind = enum  # 異なるノードタイプ
    nkInt,          # 整数値を持つ葉
    nkFloat,        # 浮動小数点の葉
    nkString,       # 文字列の値を持つ葉
    nkAdd,          # an addition
    nkSub,          # a subtraction
    nkIf            # an if statement
  Node = ref object
    case kind: NodeKind  # `kind` フィールドが判別子となる
    of nkInt: intVal: int
    of nkFloat: floatVal: float
    of nkString: strVal: string
    of nkAdd, nkSub:
      leftOp, rightOp: Node
    of nkIf:
      condition, thenPart, elsePart: Node

var n = Node(kind: nkFloat, floatVal: 1.0)
# n.kind の値が適合しないため、次のステートメントで `FieldDefect` 例外を発生させます。
n.strVal = ""
```

この例からわかるように、オブジェクト階層の利点は、異なるオブジェクトタイプ間の変換が不要であることです。しかし、無効なオブジェクトフィールドにアクセスすると、例外が発生します。

### Method call syntax

There is a syntactic sugar for calling routines: The syntax `obj.methodName(args)` can be used instead of `methodName(obj, args)`. If there are no remaining arguments, the parentheses can be omitted: `obj.len` (instead of `len(obj)`).

This method call syntax is not restricted to objects, it can be used for any type:

```Nim
import std/strutils

echo "abc".len # is the same as echo len("abc")
echo "abc".toUpperAscii()
echo({'a', 'b', 'c'}.card)
stdout.writeLine("Hallo") # the same as writeLine(stdout, "Hallo")
```

(Another way to look at the method call syntax is that it provides the missing postfix notation.)

So "pure object oriented" code is easy to write:

```Nim
import std/[strutils, sequtils]

stdout.writeLine("Give a list of numbers (separated by spaces): ")
stdout.write(stdin.readLine.splitWhitespace.map(parseInt).max.`$`)
stdout.writeLine(" is the maximum!")
```

### Properties

As the above example shows, Nim has no need for *get-properties*: Ordinary get-procedures that are called with the *method call syntax* achieve the same. But setting a value is different; for this a special setter syntax is needed:

```Nim
type
  Socket* = ref object of RootObj
    h: int # cannot be accessed from the outside of the module due to missing star

proc `host=`*(s: var Socket, value: int) {.inline.} =
  ## setter of host address
  s.h = value

proc host*(s: Socket): int {.inline.} =
  ## getter of host address
  s.h

var s: Socket
new s
s.host = 34  # same as `host=`(s, 34)
```

(The example also shows `inline` procedures.)

The `[]` array access operator can be overloaded to provide `array properties`:

```Nim
type
  Vector* = object
    x, y, z: float

proc `[]=`* (v: var Vector, i: int, value: float) =
  # setter
  case i
  of 0: v.x = value
  of 1: v.y = value
  of 2: v.z = value
  else: assert(false)

proc `[]`* (v: Vector, i: int): float =
  # getter
  case i
  of 0: result = v.x
  of 1: result = v.y
  of 2: result = v.z
  else: assert(false)
```

The example is silly, since a vector is better modelled by a tuple which already provides `v[]` access.

### Dynamic dispatch

Procedures always use static dispatch. For dynamic dispatch replace the `proc` keyword by `method`:

```Nim
type
  Expression = ref object of RootObj ## abstract base class for an expression
  Literal = ref object of Expression
    x: int
  PlusExpr = ref object of Expression
    a, b: Expression

# watch out: 'eval' relies on dynamic binding
method eval(e: Expression): int {.base.} =
  # override this base method
  quit "to override!"

method eval(e: Literal): int = e.x
method eval(e: PlusExpr): int = eval(e.a) + eval(e.b)

proc newLit(x: int): Literal = Literal(x: x)
proc newPlus(a, b: Expression): PlusExpr = PlusExpr(a: a, b: b)

echo eval(newPlus(newPlus(newLit(1), newLit(2)), newLit(4)))
```

Note that in the example the constructors `newLit` and `newPlus` are procs because it makes more sense for them to use static binding, but `eval` is a method because it requires dynamic binding.

**Note:** Starting from Nim 0.20, to use multi-methods one must explicitly pass `--multimethods:on` when compiling.

In a multi-method all parameters that have an object type are used for the dispatching:

```Nim
type
  Thing = ref object of RootObj
  Unit = ref object of Thing
    x: int

method collide(a, b: Thing) {.inline.} =
  quit "to override!"

method collide(a: Thing, b: Unit) {.inline.} =
  echo "1"

method collide(a: Unit, b: Thing) {.inline.} =
  echo "2"

var a, b: Unit
new a
new b
collide(a, b) # output: 2
```

As the example demonstrates, invocation of a multi-method cannot be ambiguous: Collide 2 is preferred over collide 1 because the resolution works from left to right. Thus `Unit, Thing` is preferred over `Thing, Unit`.

**Performance note**: Nim does not produce a virtual method table, but generates dispatch trees. This avoids the expensive indirect branch for method calls and enables inlining. However, other optimizations like compile time evaluation or dead code elimination do not work with methods.

## Exceptions

In Nim exceptions are objects. By convention, exception types are suffixed with 'Error'. The [system](system.html) module defines an exception hierarchy that you might want to stick to. Exceptions derive from `system.Exception`, which provides the common interface.

Exceptions have to be allocated on the heap because their lifetime is unknown. The compiler will prevent you from raising an exception created on the stack. All raised exceptions should at least specify the reason for being raised in the `msg` field.

A convention is that exceptions should be raised in *exceptional* cases, they should not be used as an alternative method of control flow.

### Raise statement

Raising an exception is done with the `raise` statement:

```Nim
var
  e: ref OSError
new(e)
e.msg = "the request to the OS failed"
raise e
```

If the `raise` keyword is not followed by an expression, the last exception is *re-raised*. For the purpose of avoiding repeating this common code pattern, the template `newException` in the `system` module can be used:

``` nim
raise newException(OSError, "the request to the OS failed")
```

### Try statement

The `try` statement handles exceptions:

```Nim
from std/strutils import parseInt

# read the first two lines of a text file that should contain numbers
# and tries to add them
var
  f: File
if open(f, "numbers.txt"):
  try:
    let a = readLine(f)
    let b = readLine(f)
    echo "sum: ", parseInt(a) + parseInt(b)
  except OverflowDefect:
    echo "overflow!"
  except ValueError:
    echo "could not convert string to integer"
  except IOError:
    echo "IO error!"
  except:
    echo "Unknown exception!"
    # reraise the unknown exception:
    raise
  finally:
    close(f)
```

The statements after the `try` are executed unless an exception is raised. Then the appropriate `except` part is executed.

The empty `except` part is executed if there is an exception that is not explicitly listed. It is similar to an `else` part in `if` statements.

If there is a `finally` part, it is always executed after the exception handlers.

The exception is *consumed* in an `except` part. If an exception is not handled, it is propagated through the call stack. This means that often the rest of the procedure - that is not within a `finally` clause - is not executed (if an exception occurs).

If you need to *access* the actual exception object or message inside an `except` branch you can use the [getCurrentException()](system.html#getCurrentException) and [getCurrentExceptionMsg()](system.html#getCurrentExceptionMsg) procs from the [system](system.html) module. Example:

``` nim
try:
  doSomethingHere()
except:
  let
    e = getCurrentException()
    msg = getCurrentExceptionMsg()
  echo "Got exception ", repr(e), " with message ", msg
```

### Annotating procs with raised exceptions

Through the use of the optional `{.raises.}` pragma you can specify that a proc is meant to raise a specific set of exceptions, or none at all. If the `{.raises.}` pragma is used, the compiler will verify that this is true. For instance, if you specify that a proc raises `IOError`, and at some point it (or one of the procs it calls) starts raising a new exception the compiler will prevent that proc from compiling. Usage example:

``` nim
proc complexProc() {.raises: [IOError, ArithmeticDefect].} =
  ...
proc simpleProc() {.raises: [].} =
  ...
```

Once you have code like this in place, if the list of raised exception changes the compiler will stop with an error specifying the line of the proc which stopped validating the pragma and the raised exception not being caught, along with the file and line where the uncaught exception is being raised, which may help you locate the offending code which has changed.

If you want to add the `{.raises.}` pragma to existing code, the compiler can also help you. You can add the `{.effects.}` pragma statement to your proc and the compiler will output all inferred effects up to that point (exception tracking is part of Nim's effect system). Another more roundabout way to find out the list of exceptions raised by a proc is to use the Nim `doc` command which generates documentation for a whole module and decorates all procs with the list of raised exceptions. You can read more about Nim's [effect system and related pragmas in the manual](manual.html#effect-system).

## Generics

Generics are Nim's means to parametrize procs, iterators or types with `type parameters`. Generic parameters are written within square brackets, for example `Foo[T]`. They are most useful for efficient type safe containers:

```Nim
type
  BinaryTree*[T] = ref object # BinaryTree is a generic type with
                              # generic param `T`
    le, ri: BinaryTree[T]     # left and right subtrees; may be nil
    data: T                   # the data stored in a node

proc newNode*[T](data: T): BinaryTree[T] =
  # constructor for a node
  new(result)
  result.data = data

proc add*[T](root: var BinaryTree[T], n: BinaryTree[T]) =
  # insert a node into the tree
  if root == nil:
    root = n
  else:
    var it = root
    while it != nil:
      # compare the data items; uses the generic `cmp` proc
      # that works for any type that has a `==` and `<` operator
      var c = cmp(it.data, n.data)
      if c < 0:
        if it.le == nil:
          it.le = n
          return
        it = it.le
      else:
        if it.ri == nil:
          it.ri = n
          return
        it = it.ri

proc add*[T](root: var BinaryTree[T], data: T) =
  # convenience proc:
  add(root, newNode(data))

iterator preorder*[T](root: BinaryTree[T]): T =
  # Preorder traversal of a binary tree.
  # This uses an explicit stack (which is more efficient than
  # a recursive iterator factory).
  var stack: seq[BinaryTree[T]] = @[root]
  while stack.len > 0:
    var n = stack.pop()
    while n != nil:
      yield n.data
      add(stack, n.ri)  # push right subtree onto the stack
      n = n.le          # and follow the left pointer

var
  root: BinaryTree[string] # instantiate a BinaryTree with `string`
add(root, newNode("hello")) # instantiates `newNode` and `add`
add(root, "world")          # instantiates the second `add` proc
for str in preorder(root):
  stdout.writeLine(str)
```


The example shows a generic binary tree. Depending on context, the brackets are used either to introduce type parameters or to instantiate a generic proc, iterator or type. As the example shows, generics work with overloading: the best match of `add` is used. The built-in `add` procedure for sequences is not hidden and is used in the `preorder` iterator.

There is a special `[:T]` syntax when using generics with the method call syntax:

```Nim
proc foo[T](i: T) =
  discard

var i: int

# i.foo[int]() # Error: expression 'foo(i)' has no type (or is ambiguous)

i.foo[:int]() # Success
```

## Templates

Templates are a simple substitution mechanism that operates on Nim's abstract syntax trees. Templates are processed in the semantic pass of the compiler. They integrate well with the rest of the language and share none of C's preprocessor macros flaws.

To *invoke* a template, call it like a procedure.

Example:

``` nim
template `!=` (a, b: untyped): untyped =
  # this definition exists in the System module
  not (a == b)

assert(5 != 6) # the compiler rewrites that to: assert(not (5 == 6))
```

The `!=`, `>`, `>=`, `in`, `notin`, `isnot` operators are in fact templates: this has the benefit that if you overload the `==` operator, the `!=` operator is available automatically and does the right thing. (Except for IEEE floating point numbers - NaN breaks basic boolean logic.)

`a > b` is transformed into `b < a`. `a in b` is transformed into `contains(b, a)`. `notin` and `isnot` have the obvious meanings. 

Templates are especially useful for lazy evaluation purposes. Consider a simple proc for logging:

```Nim
const
  debug = true

proc log(msg: string) {.inline.} =
  if debug: stdout.writeLine(msg)

var
  x = 4
log("x has the value: " & $x)
```

This code has a shortcoming: if `debug` is set to false someday, the quite expensive `$` and `&` operations are still performed! (The argument evaluation for procedures is *eager*).

Turning the `log` proc into a template solves this problem:

```Nim
const
  debug = true

template log(msg: string) =
  if debug: stdout.writeLine(msg)

var
  x = 4
log("x has the value: " & $x)
```

The parameters' types can be ordinary types or the meta types `untyped`, `typed`, or `type`. `type` suggests that only a type symbol may be given as an argument, and `untyped` means symbol lookups and type resolution is not performed before the expression is passed to the template.

If the template has no explicit return type, `void` is used for consistency with procs and methods.

To pass a block of statements to a template, use `untyped` for the last parameter:

```Nim
template withFile(f: untyped, filename: string, mode: FileMode,
                  body: untyped) =
  let fn = filename
  var f: File
  if open(f, fn, mode):
    try:
      body
    finally:
      close(f)
  else:
    quit("cannot open: " & fn)

withFile(txt, "ttempl3.txt", fmWrite):
  txt.writeLine("line 1")
  txt.writeLine("line 2")
```

In the example the two `writeLine` statements are bound to the `body` parameter. The `withFile` template contains boilerplate code and helps to avoid a common bug: to forget to close the file. Note how the `let fn = filename` statement ensures that `filename` is evaluated only once.

### Example: Lifting Procs

```Nim
import std/math

template liftScalarProc(fname) =
  ## Lift a proc taking one scalar parameter and returning a
  ## scalar value (eg `proc sssss[T](x: T): float`),
  ## to provide templated procs that can handle a single
  ## parameter of seq[T] or nested seq[seq[]] or the same type
  ##
  ## .. code-block:: Nim
  ##  liftScalarProc(abs)
  ##  # now abs(@[@[1,-2], @[-2,-3]]) == @[@[1,2], @[2,3]]
  proc fname[T](x: openarray[T]): auto =
    var temp: T
    type outType = typeof(fname(temp))
    result = newSeq[outType](x.len)
    for i in 0..<x.len:
      result[i] = fname(x[i])

liftScalarProc(sqrt)   # make sqrt() work for sequences
echo sqrt(@[4.0, 16.0, 25.0, 36.0])   # => @[2.0, 4.0, 5.0, 6.0]
```

## Compilation to JavaScript

Nim code can be compiled to JavaScript. However in order to write JavaScript-compatible code you should remember the following:
- `addr` and `ptr` have slightly different semantic meaning in JavaScript. It is recommended to avoid those if you're not sure how they are translated to JavaScript.
- `cast[T](x)` in JavaScript is translated to `(x)`, except for casting between signed/unsigned ints, in which case it behaves as static cast in C language. 
- `cstring` in JavaScript means JavaScript string. It is a good practice to use `cstring` only when it is semantically appropriate. E.g. don't use `cstring` as a binary data buffer.

## Part 3

The next part is entirely about metaprogramming via macros: [Part III](tut3.html)
