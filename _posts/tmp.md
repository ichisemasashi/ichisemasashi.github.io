
# LLisp: Lisp in Lisp



å…ˆé€±ã€ç§ã¯ã‚ã‚‹ã“ã¨ã‚’è€ƒãˆãŸã€‚"ç§ãŒæ›¸ã‘ã‚‹æœ€ã‚‚ç°¡å˜ãªLispã‚¤ãƒ³ã‚¿ãƒ—ãƒªã‚¿ã§ã€ãƒžã‚¯ãƒ­ã‚’ã‚µãƒãƒ¼ãƒˆã—ã¦ã„ã‚‹ã‚‚ã®ã¯ä½•ã‹ï¼Ÿ"

é€±æœ«ã«Clojureã‚’ãƒãƒƒã‚¯ã—ã€ã„ãã¤ã‹ã®æ··ä¹±ã‚’çµŒã¦ã€REPLãŒèª•ç”Ÿã—ã¾ã—ãŸã€‚ ã“ã®ã‚¨ãƒƒã‚»ã‚¤ã§ã¯ã€åŒã˜æ—…ã‚’çµŒã¦ã€Clojureã§ãƒžã‚¯ãƒ­ã‚’ã‚µãƒãƒ¼ãƒˆã™ã‚‹ç‹¬è‡ªã®Lispã‚¤ãƒ³ã‚¿ãƒ—ãƒªã‚¿ã‚’ä½œã£ã¦ã¿ã¾ã—ã‚‡ã†! LLisp: a lisp in a lispã¨ã§ã‚‚å‘¼ã³ã¾ã—ã‚‡ã†ã‹ã€‚

æœ€å¾Œã«ã€ã‚ãªãŸã¯è‡ªåˆ†ã®ãƒžã‚¯ãƒ­ã‚’è‡ªåˆ†ã®ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°è¨€èªžã§æ›¸ãã“ã¨ãŒã§ãã¾ã™ã€‚ã•ã‚ã€å§‹ã‚ã¾ã—ã‚‡ã†ã€‚

# ã‚¹ã‚±ãƒƒãƒ

ã•ã¦ã€ã“ã‚Œã‹ã‚‰è¡Œã†ã¹ãã“ã¨ã®åŸºæœ¬çš„ãªè€ƒãˆæ–¹ã‚’ã‚¹ã‚±ãƒƒãƒã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚


![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc1NC1lMTg0NmZlNC0yM2I3LTQ3ZTctYjJmNy1lMjJiMDJlZmU2NmQucG5n){style="max-width:100%"}


ãƒ—ãƒ­ã‚°ãƒ©ãƒžãƒ¼ãŒä½•ã‹ã‚³ãƒ¼ãƒ‰ã‚’æ›¸ãã€‚ã“ã“ã¾ã§ã¯å˜ãªã‚‹ãƒ†ã‚­ã‚¹ãƒˆã§ã€ç§ãŸã¡ãŒã§ãã‚‹ã“ã¨ã¯ã‚ã¾ã‚Šãªã„ã€‚ç§ãŸã¡ã¯ãƒ—ãƒ­ã‚°ãƒ©ãƒžãƒ¼ã®æ›¸ã„ãŸãƒ†ã‚­ã‚¹ãƒˆã‚’èª­ã¿ã€ãã‚Œã‚’ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã«å¤‰æ›ã™ã‚‹ã€‚ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã‚’è©•ä¾¡ã—ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒžãŒæ„å›³ã—ãŸã‚‚ã®ã‚’è¿”ã™ã“ã¨ãŒã§ãã‚‹ã®ã ã€‚

ã‚‚ã—ã€`"(+ 1 1)"` ã‚’ `2` ã«å¤‰æ›ã™ã‚‹ã“ã¨ãŒã§ãã‚Œã°ã€ç§ãŸã¡ã¯ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°è¨€èªžã®ãƒ«ãƒ¼ãƒ„ã‚’æ‰‹ã«å…¥ã‚Œã‚‹ã“ã¨ãŒã§ãã‚‹ã®ã§ã™ã€‚

# Reader

2.èª­ã¿ã€ã‚’å‡¦ç†ã—ã¦ã¿ã‚ˆã†ã€‚ãƒ—ãƒ­ã‚°ãƒ©ãƒžãŒ `"(+ 1 1)"` ã®ã‚ˆã†ãªãƒ†ã‚­ã‚¹ãƒˆã‚’æ›¸ã„ãŸã‚‰ã€ãã‚Œã‚’ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã«å¤‰æ›ã—ã¦ã‚ã’ãŸã„ã€‚ã¿ãŸã„ãªæ„Ÿã˜ã€‚

```
(read-string "(+ 1 1)")
; =>
(+ 1 1)
```

è‡ªåˆ†ãŸã¡ã§ `read-string` ã‚’æ›¸ãã“ã¨ã¯ *å¯èƒ½* ã§ã™ã€‚æœ€ã‚‚å˜ç´”ãªã‚±ãƒ¼ã‚¹ã§ã¯ã€ãã‚Œã¯éžå¸¸ã«ç°¡å˜ã§ã™^\[1\]^ã€‚ã—ã‹ã—ã€æˆ‘ã€…ã¯çµå±€Clojureã«ã„ã‚‹ã€‚ãã—ã¦ã€Clojureã¯ã™ã§ã«Lispã‚³ãƒ¼ãƒ‰ã®èª­ã¿æ–¹ã‚’ç†è§£ã—ã¦ã„ã‚‹ã€‚ã‚ºãƒ«ã—ã¦Clojureã® `edn` ã‚’ä½¿ãŠã†ã€‚

```
(ns simple-lisp.core
  (:refer-clojure :exclude [eval read read-string])
  (:require [clojure.edn :refer [read read-string]]))
```

ãã—ã¦ã€ã»ã‚‰ã€`read-string` ã¯ç§ãŸã¡ãŒæœ›ã‚€ã“ã¨ã‚’ã—ã¾ã™ã€‚

```
(read-string "(+ 1 1)")
; =>
(+ 1 1)
```

# Evaluation

ã•ã¦ã€æ¬¡ã¯ã€Œ3.è©•ä¾¡ã€ã§ã™ã€‚æˆ‘ã€…ã®ã‚´ãƒ¼ãƒ«ã¯ãƒ‡ãƒ¼ã‚¿æ§‹é€ ã‚’å—ã‘å–ã‚Šã€ãã‚Œã‚’è©•ä¾¡ã—ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒžãƒ¼ãŒæ„å›³ã—ãŸã‚‚ã®ã‚’è¿”ã™ã“ã¨ã ã€‚ã¾ãšã¯ã€ã“ã‚“ãªæ„Ÿã˜ã®ç°¡å˜ãªé–¢æ•°ã‹ã‚‰å§‹ã‚ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```
(defn eval [form] 
  ;; TODO 
  )
```

## ãƒªãƒ†ãƒ©ãƒ«ã‚’è©•ä¾¡ã™ã‚‹

ç°¡å˜ãªã“ã¨ã‹ã‚‰å§‹ã‚ã¾ã—ã‚‡ã†ã€‚ä¾‹ãˆã°ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒžãŒ `12` ã¨æ›¸ã„ãŸã‚‰ã€ `12` ã¨ã„ã†æ•°å­—ã‚’è¿”ã™ã ã‘ã§ã‚ˆã„ã®ã§ã™ã€‚ ã“ã‚Œã¯æ–‡å­—åˆ—(`"foo"`)ã€æ–‡å­—(`\b`)ã€ãƒ–ãƒ¼ãƒªã‚¢ãƒ³(`true`)ã«ã¤ã„ã¦ã‚‚åŒã˜ã“ã¨ãŒè¨€ãˆã¾ã™ã€‚ ã“ã‚Œã‚‰ã¯ãƒªãƒ†ãƒ©ãƒ«*.*ã§ã™ã€‚

ã“ã“ã§ã€ãƒªãƒ†ãƒ©ãƒ«ã‚’è©•ä¾¡ã™ã‚‹æ–¹æ³•ã‚’èª¬æ˜Žã—ã¾ã™ã€‚ã¾ãšã€ãã‚Œã‚‰ã‚’æ¤œå‡ºã—ã¾ã—ã‚‡ã†ã€‚

```
(def literal?
  (some-fn string? number? boolean? char? nil?))
```

ã‚‚ã—ã‚ãªãŸãŒ `some-fn` ã‚’è¦‹ãŸã“ã¨ãŒãªã‘ã‚Œã°ã€ã“ã‚Œã¯ clojure ã®æ•°ã‚ã‚‹ `ä¾¿åˆ©ã§å¼·åŠ›ãª` ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ã®ã†ã¡ã®ä¸€ã¤ã§ã™ã€‚ã“ã‚Œã§ `eval` ã§ãƒªãƒ†ãƒ©ãƒ«ã‚’æ‰±ãˆã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã—ãŸã€‚

```
(defn eval [form]
  (cond
    (literal? form) form))
```

ãã—ã¦ã€ç§ãŸã¡ã®ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°è¨€èªžãŒä½•ã‹ã‚’ã—å§‹ã‚ã‚‹ã®ã§ã™ã€‚

```
(eval (read-string "12"))
; => 12
```

## å¼•ç”¨ã®ç´¹ä»‹

ãã‚Œã§ã¯ã€`quote`ã«ã¤ã„ã¦èª¬æ˜Žã—ã¾ã™ã€‚ quoteã¯Lispã§ã¯éžå¸¸ã«é‡è¦ãªå½¢å¼ã§ã™ã€‚ã“ã‚Œã«ã‚ˆã‚Šã€è©•ä¾¡ã•ã‚Œãªã„ã‚³ãƒ¼ãƒ‰ã‚’æ›¸ãã“ã¨ãŒã§ãã¾ã™ã€‚ä¾‹ãˆã°

```
(quote (+ 1 1))
; => 
(+ 1 1)
```

`(+ 1 1)` ã¯ `quote` ã®ä¸­ã«ã‚ã‚‹ã®ã§ã€ãã‚Œã‚’è©•ä¾¡ã—ã¦ `2` ã‚’è¿”ã™ã“ã¨ã¯ã—ã¾ã›ã‚“ã§ã—ãŸã€‚ ãã®ä»£ã‚ã‚Šã€å¼ãã®ã‚‚ã®ã‚’è¿”ã—ã¾ã™ã€‚ã“ã‚Œã¯å¼•ç”¨ç¬¦ã‚’ã‚µãƒãƒ¼ãƒˆã—ãªã„è¨€èªžã‹ã‚‰æ¥ãŸäººã«ã¯å¥‡å¦™ã«æ€ãˆã‚‹ã‹ã‚‚ã—ã‚Œã¾ã›ã‚“ãŒã€Lispã§ã¯å¸¸ã«ä½¿ç”¨ã•ã‚Œã¦ã„ã‚‹æ–¹æ³•ã§ã™ã€‚å®Ÿéš›ã€ãã®ãŸã‚ã®ã‚·ãƒ§ãƒ¼ãƒˆã‚«ãƒƒãƒˆãŒã‚ã‚Šã¾ã™ã€‚

```
'(+ 1 1) 
```

ã“ã‚Œã‚’èª­ã¿è¾¼ã‚€ã¨ã€æ¬¡ã®ã‚ˆã†ã«å¤‰æ›ã•ã‚Œã¾ã™ã€‚

```
(quote (+ 1 1))
```

ã‚ã‚ŠãŒãŸã„ã“ã¨ã«ã€edn ã® `read-string` ãŒã“ã‚Œã‚’ä»£è¡Œã—ã¦ãã‚Œã¾ã™ã€‚ ðŸ™‚

## ã‚¯ã‚©ãƒ¼ãƒˆã®è©•ä¾¡

ã§ã¯ã€ã©ã®ã‚ˆã†ã«ã—ã¦ã‚¯ã‚©ãƒ¼ãƒˆã‚’è©•ä¾¡ã™ã‚‹ã®ã§ã—ã‚‡ã†ã‹ã€‚ã¾ãšã€ã‚¯ã‚©ãƒ¼ãƒˆã‚’æ¤œå‡ºã™ã‚‹é–¢æ•°ã‚’ä½œã£ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```
(defn seq-starts-with? [starts-with form]
  (and (seqable? form) (= (first form) starts-with)))

(def quote? (partial seq-starts-with? 'quote))
```

ãƒ•ã‚©ãƒ¼ãƒ ãŒãƒªã‚¹ãƒˆã§ã€æœ€åˆã®è¦ç´ ãŒã‚·ãƒ³ãƒœãƒ« `quote` ã§ã‚ã‚‹ã‹ã©ã†ã‹ã‚’ç¢ºèªã—ã¾ã™ã€‚

```
(quote? (read-string "'(+ 1 1)"))
; => 
true
```

é­…åŠ›çš„ãªã‚ˆã†ã«å‹•ä½œã—ã¾ã™ã€‚ã§ã¯ã€`eval`ã‚’æ›´æ–°ã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```
(defn eval [form]
  (cond
    (literal? form) form
    (quote? form) (second form)))
```

ãƒ•ã‚©ãƒ¼ãƒ ãŒ `quote` ã§ã‚ã‚Œã°ã€ãã®ã€Œsecondã€éƒ¨åˆ†ã‚’è¿”ã—ã¾ã™ã€‚ãã—ã¦å‡ºæ¥ä¸ŠãŒã‚Šã€‚

```
(eval (read-string "(quote (+ 1 1))"))
; => (+ 1 1)
```

## ã‚·ãƒ³ãƒœãƒ«ã®ç´¹ä»‹

æ¬¡ã«ã‚·ãƒ³ãƒœãƒ«ã«ã¤ã„ã¦èª¬æ˜Žã—ã¾ã™ã€‚ã‚·ãƒ³ãƒœãƒ«ã¯Lispã®ç‰¹æ®Šãªãƒ‡ãƒ¼ã‚¿åž‹ã§ã™ã€‚å¤‰æ•°ã¨åŒã˜ã§ã€ã‚·ãƒ³ãƒœãƒ«ã‚’è©•ä¾¡ã™ã‚‹ã¨ãã«ã€ãã‚Œã‚’èª¿ã¹ã‚‹ã¨ã„ã†æ„å‘³ã§ã™ã€‚



![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc2MS01YmQ2ODhlOS00ZDEzLTQ3ZjMtYTg4My1lYTZhMTdlYWRjNjEucG5n){style="max-width:100%"}


ã©ã“ã§èª¿ã¹ã‚‹ã‚“ã ï¼Ÿã‚¤ãƒ³ã‚¿ãƒ¼ãƒ—ãƒªã‚¿ã«ã¯ã‚ã‚‹ç¨®ã® `environment` ãŒå¿…è¦ã§ã€ãã‚Œã¯ç§ãŸã¡ãŒå®šç¾©ã—ãŸã™ã¹ã¦ã®å¤‰æ•°ã‚’è¿½è·¡ã—ã¾ã™ã€‚ä¾‹ãˆã°ã€`fav-num` ã¨ã„ã†ã‚·ãƒ³ãƒœãƒ«ãŒ `41` ã‚’æŒ‡ã—ã¦ã„ã‚‹ã‚ˆã†ãªç’°å¢ƒãŒã‚ã£ãŸå ´åˆã€`fav-num` ã‚’è©•ä¾¡ã™ã‚‹ã¨æ¬¡ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

```
(eval env (read-string "fav-num"))
; => 
41
```

## ã‚·ãƒ³ãƒœãƒ«ã‚’è©•ä¾¡ã™ã‚‹

ã¾ãšã€ã“ã® `environment` ã‚’ä½œæˆã—ã¾ã—ã‚‡ã†ã€‚ ã‚·ãƒ³ãƒœãƒ«ã¨ãã®å€¤ã®å¯¾å¿œé–¢ä¿‚ã‚’ä¿æŒã™ã‚‹ãŸã‚ã«ã€java ã® `Hashmap` ã‚’åˆ©ç”¨ã™ã‚‹ã“ã¨ãŒã§ãã‚‹ã€‚

java ã® Hashmap ã‚’ã‚¤ãƒ³ãƒãƒ¼ãƒˆã—ã‚ˆã†ã€‚

```
(ns simple-lisp.core
  (:refer-clojure :exclude [eval read read-string])
  (:require [clojure.edn :refer [read read-string]])
  (:import (java.util HashMap)))
```

ãã—ã¦ã€`env` ãƒžãƒƒãƒ—ã‚’ä½œæˆã™ã‚‹ãŸã‚ã®ç°¡å˜ãªé–¢æ•°ã‚’ä½œæˆã—ã¾ã™ã€‚

```
(defn env [] {:globe (HashMap. {'fav-num 41})})
```

ã“ã‚Œã§ `eval` ã§ `env` ãƒžãƒƒãƒ—ã‚’å—ã‘å–ã‚Šã€ã‚·ãƒ³ãƒœãƒ«ã‚’æ‰±ã†ã“ã¨ãŒã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã—ãŸã€‚

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)))
```

Clojureã«ã¯ã™ã§ã«ä¾¿åˆ©ãª `symbol?` ã¨ã„ã†é–¢æ•°ãŒã‚ã‚Šã¾ã™ã€‚ãã‚ŒãŒtrueãªã‚‰ã€ `lookup-symbol` ã¨ã—ã¾ã™ã€‚ ä»¥ä¸‹ã¯ `lookup-symbol` ãŒã©ã®ã‚ˆã†ã«è¦‹ãˆã‚‹ã‹ã§ã™ã€‚

```
(defn lookup-symbol [{:keys [globe]} sym]
  (let [v (when (contains? globe sym) [(get globe sym)])]
    (assert v (format "expected value for sym = %s" sym))
    (first v)))
```

ãã—ã¦ã€ã‚·ãƒ³ãƒœãƒ«ãŒæ©Ÿèƒ½ã™ã‚‹!

```
(eval (env) (read-string "fav-num"))
; => 
41
```

## ãƒ—ãƒªãƒŸãƒ†ã‚£ãƒ–ã®è©•ä¾¡

æ¬¡ã¯ã€`(+ 1 1)` ã‚’å‹•ä½œã•ã›ã‚‹ãŸã‚ã®é‡è¦ãªã‚¹ãƒ†ãƒƒãƒ—ã§ã™ã€‚ç§ãŸã¡ã¯ `+` ãŒä½•ã‹ã‚’æ„å‘³ã™ã‚‹ã‚ˆã†ã«ã—ãŸã„ã¨æ€ã†ã§ã—ã‚‡ã†ã€‚ã‚·ãƒ³ãƒœãƒ« `+` ã¯ä½•ã‚’æŒ‡ã™ã¹ãã§ã—ã‚‡ã†ã‹ï¼Ÿ

Clojureã®é–¢æ•°ã¯ã©ã†ã§ã—ã‚‡ã†ã‹ï¼Ÿ

```
(defn env [] {:globe (HashMap. {'+ + 'fav-num 41})})
```

ãã†ã™ã‚‹ã¨ã€ä»Šåº¦ã¯ã‚·ãƒ³ãƒœãƒ« `+` ã‚’è©•ä¾¡ã™ã‚‹ãŸã³ã« clojure ã®é–¢æ•°ãŒè¿”ã•ã‚Œã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

```
(eval (env) (read-string "+"))
; => #object[clojure.core$_PLUS_ 0x4efcb4b5 "[emailÂ protected]"]
```

ã¾ãŸã€evalã§clojureã®é–¢æ•°ã‚’ã€Œå—ã‘å–ã£ãŸã€å ´åˆã€ãã‚Œã‚’ãƒªãƒ†ãƒ©ãƒ«ã¨ã—ã¦æ‰±ãˆã°ã„ã„ã ã‘ã§ã™ã€‚

```
(def literal?
  (some-fn string? number? boolean? char? nil? fn?))
```

```
(eval (env) +)
; => #object[clojure.core$_PLUS_ 0x4efcb4b5 "[emailÂ protected]"]
```

ã“ã‚Œã¯ç§ãŸã¡ã®ã‚¨ãƒƒã‚»ã‚¤ã§ã¯å°ã•ãªä¸€æ­©ã§ã™ãŒã€ç§ãŸã¡ã®Lispã§ã¯å¤§ããªä¸€æ­©ã§ã™ã€‚ ã“ã‚Œã§ `(+ 1 1)` ã®ã™ã¹ã¦ã®è¦ç´ ãŒè©•ä¾¡ã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã—ãŸã€‚

## åŽŸå§‹çš„ãªã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³

ã§ã¯ã€ã“ã‚Œã‚‰ã®é–¢æ•°ã‚’ã©ã®ã‚ˆã†ã«å®Ÿè¡Œã™ã‚Œã°ã‚ˆã„ã®ã§ã—ã‚‡ã†ã‹ï¼Ÿã§ã¯ã€ `eval` ã‚’æ›´æ–°ã—ã¦ã€ `(+ 1 1)` ã®ã‚ˆã†ãªãƒªã‚¹ãƒˆã‚’æ‰±ãˆã‚‹ã‚ˆã†ã«ã—ã¾ã—ã‚‡ã†ã€‚

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)
    :else (eval-application env form)))
```

ç°¡å˜ã ã€‚ã‚‚ã— `form` ãŒä½•ã§ã‚ã‚‹ã‹ç†è§£ã§ããªã‘ã‚Œã°ã€ãã‚Œã¯ãƒªã‚¹ãƒˆã§ã‚ã‚Šã€ãã‚Œã‚’å®Ÿè¡Œã—ãŸã„ã“ã¨ã‚’æ„å‘³ã™ã‚‹ã«é•ã„ã‚ã‚Šã¾ã›ã‚“! ã“ã‚Œã¯ `eval-application` ãŒã©ã®ã‚ˆã†ã«å‹•ä½œã™ã‚‹ã‹ã‚’ç¤ºã—ã¦ã„ã¾ã™ã€‚

```
(defn eval-application [env [f & args]]
  (let [f-evaled (eval env f)]
    (cond
      (fn? f-evaled) (apply f-evaled (eval-many env args)))))
```

`eval-application` ã§ã¯ã€ãƒªã‚¹ãƒˆã® *æœ€åˆã®* éƒ¨åˆ†ã‚’è©•ä¾¡ã—ã¾ã™ã€‚ã“ã®ä¾‹ã§ã¯ã€ã‚·ãƒ³ãƒœãƒ« `+` ãŒè©•ä¾¡ã•ã‚Œã€clojureé–¢æ•°ãŒè¿”ã•ã‚Œã¾ã™ã€‚

ã‚‚ã— `f-evaled` ãŒ clojure ã®é–¢æ•°ã§ã‚ã‚Œã°(å®Ÿéš›ãã†ã§ã™)ã€æ¬¡ã®ã‚ˆã†ã«å®Ÿè¡Œã•ã‚Œã¾ã™ã€‚

```
(apply f-evaled (eval-many env args))
```

`eval-many` ã¯ã€ãƒ•ã‚©ãƒ¼ãƒ ã®ãƒªã‚¹ãƒˆã‚’è©•ä¾¡ã™ã‚‹ãŸã‚ã®å˜ãªã‚‹ãƒ˜ãƒ«ãƒ‘ãƒ¼ã§ã™ã€‚

```
(defn eval-many [e forms] (map (partial eval e) forms))
```

`args` ã¯ `(1 1)` ã¨ãªã‚‹ã®ã§ã€ `eval-many` ã¯ `(1 1)` ã‚’è¿”ã™ã“ã¨ã«ãªã‚Šã¾ã™ã€‚

```
(apply + '(1 1))
```

ã™ã‚‹ã¨ã€`2`ãŒè¿”ã•ã‚Œã€`(+ 1 1)`ãŒå‹•ä½œã™ã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™!

```
(eval (env) (read-string "(+ 1 1)")
; => 
2
```

å†å¸°çš„ã«è©•ä¾¡ã™ã‚‹ã®ã§ã€ã™ã§ã«è¤‡é›‘ãªã“ã¨ãŒã§ãã‚‹ã“ã¨ã‚’å¿˜ã‚Œãªã„ã§ãã ã•ã„ã€‚

```
(eval (env) (read-string "(+ 1 (+ 1 fav-num))")
; => 
43
```

## def ã®ç´¹ä»‹

OKY DOKE, æˆ‘ã€…ã¯ç’°å¢ƒã‚’æ‰‹ã«å…¥ã‚Œã€ãã®ç’°å¢ƒã‹ã‚‰ä½•ã‹ã‚’æŽ¢ã—å‡ºã™ã‚·ãƒ³ãƒœãƒ«ã‚’æ‰‹ã«å…¥ã‚Œã€ã•ã‚‰ã« `(+ 1 (+ 1 fav-num))` ã‚’è©•ä¾¡ã™ã‚‹ã“ã¨ãŒã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã—ãŸã€‚

ã—ã‹ã—ã€ã©ã†ã‚„ã£ã¦å¤‰æ•°ã‚’å®šç¾©ã™ã‚‹ã®ã§ã—ã‚‡ã†ã‹ï¼Ÿä¾‹ãˆã°ã€`second-fav`ã¨ã„ã†ã‚·ãƒ³ãƒœãƒ«ãŒ`42`ã‚’æŒ‡ã™ã‚ˆã†ã«ã—ãŸã„ã¨ã—ã¾ã—ã‚‡ã†ã€‚

ãã†ã™ã‚‹ã¨ã€ç‰¹æ®Šãªå½¢å¼ã‚’å°Žå…¥ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚ã“ã®ã‚ˆã†ãªã‚‚ã®ã§ã™ã€‚

```
(def second-fav (+ 1 fav-num))
```

ã“ã® `def` ãƒ•ã‚©ãƒ¼ãƒ ã‚’å—ã‘å–ã£ãŸã‚‰ã€ç’°å¢ƒã‚’æ›´æ–°ã—ã¦ã€ã‚·ãƒ³ãƒœãƒ« `second-fav` ã‚’ `(+ 1 fav-num)` ã®è©•ä¾¡å€¤ãŒã©ã†ãªã‚‹ã‹ã«å‘ã‘ã‚‹ã ã‘ã§ã„ã„ã®ã§ã™ã€‚

## Defã‚’è©•ä¾¡ã™ã‚‹

ã¨ã„ã†æ„Ÿã˜ã§ã™ã­ã€‚ã¾ãšã¯ã“ã®ãƒ•ã‚©ãƒ¼ãƒ ã‚’æ¤œå‡ºã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```
(def def? (partial seq-starts-with? 'def))
```

ãã—ã¦ã€ãã‚Œã‚’å‡¦ç†ã™ã‚‹ãŸã‚ã« `eval` ã‚’æ›´æ–°ã—ã¾ã™ã€‚

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)
    (def? form) (eval-def env form)
    :else (eval-application env form)))
```

ãã—ã¦ã€`eval-def`ãŒå¿…è¦ã¨ã™ã‚‹ã“ã¨ã¯ã€ã“ã‚Œã ã‘ã§ã™ã€‚

```
(defn eval-def [env [_ k v]]
  (assert (symbol? k) (format "expected k = %s to be a symbol" k))
  (.put (:globe env) k (eval env v)))
```

ã“ã“ã§ã¯ã€`def` ã®æœ€åˆã®å¼•æ•°ãŒã‚·ãƒ³ãƒœãƒ« `k` ã§ã‚ã‚‹ã“ã¨ãŒåˆ†ã‹ã£ã¦ã„ã¾ã™ã€‚ 2 ç•ªç›®ã®å¼•æ•°ã¯ä¿å­˜ã—ãŸã„ *å€¤* `v` ã§ã™ã€‚ãã“ã§ã€ç’°å¢ƒã® `globe` ãƒãƒƒã‚·ãƒ¥ãƒžãƒƒãƒ—ã‚’æ›´æ–°ã—ã¦ã€ã‚·ãƒ³ãƒœãƒ« `k` ã‚’ `(eval env v)` ã«æŒ‡å®šã—ã¾ã™ã€‚ã“ã®å ´åˆã€ `k` ã¯ `second-fav` ã§ã€ `v` ã¯ `(+ 1 fav-num)` ã¨ãªã‚Šã€ `(eval env v)` ã¯ `42` ã¨ãªã‚Šã¾ã™ã€‚

ã‹ã£ã“ã„ã„ã§ã™ã­ã€ã“ã‚Œã¯ã€‚

```
(let [e (env)] 
    (eval e (read-string "(def second-fav (+ 1 fav-num))"))
    (eval e (read-string "second-fav")))
; => 
42
```

## ifã®å°Žå…¥

ã•ã¦ã€ã‚‚ã†ä¸€ã¤ç‰¹æ®Šãªå½¢å¼ã‚’å®Ÿè£…ã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚æ¡ä»¶ä»˜ããƒ­ã‚¸ãƒƒã‚¯ã‚’å‡¦ç†ã™ã‚‹ã‚‚ã®ãŒå¿…è¦ã ã€‚

```
(if (= fav-num 41) 'yess 'noo))
```

`if` ã‚’å¾—ã‚‹ã¨ã€ãƒ†ã‚¹ãƒˆãƒ•ã‚©ãƒ¼ãƒ `(= fav-num 41)`ã‚’è©•ä¾¡ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚ã‚‚ã—ãã‚ŒãŒçœŸãªã‚‰ã€çœŸã®ã‚±ãƒ¼ã‚¹ `'yess` ã‚’è©•ä¾¡ã—ã¾ã™ã€‚ ãã†ã§ãªã‘ã‚Œã°ã€å½ã®ã‚±ãƒ¼ã‚¹ã‚’è©•ä¾¡ã™ã‚‹ã“ã¨ã«ãªã‚Šã¾ã™ã€‚`'noo` ã¨ãªã‚Šã¾ã™ã€‚ è¨ˆç”»çš„ã§ã™ã­ã€‚

## ifã®è©•ä¾¡

ifã‚’å®Ÿè£…ã™ã‚‹ãŸã‚ã«ã€ã„ã¤ã‚‚ã®ã‚ˆã†ã«ã¾ãšæ¤œå‡ºã—ã¾ã—ã‚‡ã†ã€‚

```
(def if? (partial seq-starts-with? 'if))
```

ãã—ã¦ã€ãã‚Œã‚’ `eval` ã§ä½¿ç”¨ã—ã¾ã™ã€‚

```
(defn eval [env form]
  (cond
    (literal? form) form
    (quote? form) (second form)
    (symbol? form) (lookup-symbol env form)
    (def? form) (eval-def env form)
    (if? form) (eval-if env form)
    :else (eval-application env form)))
```

ãã—ã¦ã€`eval-if`ã¯ã“ã‚“ãªæ„Ÿã˜ã§ã—ã‚‡ã†ã‹ã€‚

```
(defn eval-if [env [_ test-form when-true when-false]]
  (let [evaled-test (eval env test-form)]
    (eval env (if evaled-test when-true when-false))))
```

ã»ã‚“ã®æ•°è¡Œã€ç§ãŸã¡ã®èª¬æ˜Žã«å¿ å®Ÿã«å¾“ã£ãŸã‚‚ã®ã§ã™ã€‚`evaled-test`ãŒçœŸã§ã‚ã‚‹ã‹ã©ã†ã‹ã‚’ç¢ºèªã—ã¾ã™ã€‚ãã—ã¦ã€ `when-true` ã‹ `when-false` ã®ã©ã¡ã‚‰ã‹ã‚’è©•ä¾¡ã™ã‚‹ã€‚

ã¤ã„ã§ã«ã€ `env` ã«ç´ æ•µãªãƒ—ãƒªãƒŸãƒ†ã‚£ãƒ–é–¢æ•°ã‚’ãŸãã•ã‚“è¿½åŠ ã—ã¦ã¿ã‚ˆã†ã€‚

```
(defn env [] {:globe (HashMap. {'+ + '= = 'list list 'map map 'concat concat
                                'first first 'second second 'not not 'fav-num 41})})
```

ãã—ã¦ã€ãƒãƒ¼ãƒ³ã€‚ã“ã®å°ã•ãªã‚¤ãƒ³ã‚¿ãƒ—ãƒªã‚¿ãŒã‹ãªã‚Šå¤šãã®ã“ã¨ã‚’ã§ãã‚‹ã‚ˆã†ã«ãªã£ãŸã®ã§ã™

```
    (eval (env) (read-string "(if (= fav-num 41) 'yess 'noo)"))
    ; => yess
```

## é–¢æ•°ã®ç´¹ä»‹

ã•ã‚‰ã«ä¸Šã‚’ç›®æŒ‡ã—ã¾ã—ã‚‡ã†ã€‚Clojureã® `+` ãªã©ã¯ã‚ã‚Šã¾ã™ãŒã€ãƒ—ãƒ­ã‚°ãƒ©ãƒžãŒ *è‡ªåˆ†è‡ªèº«ã®* é–¢æ•°ã‚’æ›¸ããŸã„ã¨ã—ãŸã‚‰ã©ã†ã§ã—ã‚‡ã†ã‹ï¼Ÿã“ã“ã«ä¾‹ãŒã‚ã‚Šã¾ã™ã€‚

```
((clo (x) (+ x fav-num) 2))
```

ã“ã“ã§ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒžã¯ `x` ã‚’å—ã‘å–ã‚Šã€ãã‚Œã« `fav-num` ã‚’åŠ ãˆã‚‹é–¢æ•°ã‚’æ›¸ã„ãŸã¨ã—ã¾ã™ã€‚

å½¼ã‚‰ãŒã“ã†æ›¸ã„ãŸã¨ã—ã¾ã™ã€‚å½¼ã‚‰ã®é–¢æ•°ã‚’å®Ÿè¡Œã™ã‚‹ã«ã¯ã©ã†ã—ãŸã‚‰ã„ã„ã§ã—ã‚‡ã†ã‹ï¼Ÿã•ã¦ã€å½¼ã‚‰ã®å®šç¾©ã«ã¯ `arguments` ã¨ `body` ãŒã‚ã‚Šã¾ã™ã€‚ ã“ã®å ´åˆã€ãã‚Œãžã‚Œ `(x)` ã¨ `(+ x fav-num)` ã§ã™ã€‚ã“ã®ä¾‹ã§ã¯ã€é–¢æ•°ã¯ `2` ã§å‘¼ã³å‡ºã•ã‚Œã¾ã™ã€‚

ã‚‚ã—ã€ `(+ x fav-num)` ã‚’è©•ä¾¡ã™ã‚‹ã“ã¨ãŒã§ãã‚Œã°ã€ `x` ãŒ `2` ã‚’æŒ‡ã™ç’°å¢ƒã§ã€ã¾ã‚‹ã§é–¢æ•°ã‚’ *ran* ã—ãŸã‹ã®ã‚ˆã†ã«ãªã‚‹ã®ã§ã™! ã“ã®ã‚¢ã‚¤ãƒ‡ã‚¢ã‚’èª¬æ˜Žã™ã‚‹ãŸã‚ã®å›³ãŒã‚ã‚Šã¾ã™ã€‚


![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc2OC00MzBkN2ZjMC05MDQyLTQ0ZGUtYmYyOS0zNjM4ZjA5YTllYzkucG5n){style="max-width:100%"}


ã©ã†ã‚„ã£ã¦ `x` ã« `2` ã‚’æŒ‡å®šã™ã‚‹ã®ã§ã—ã‚‡ã†ã‹ï¼Ÿ ãªãœãªã‚‰ã€`body` ã®è©•ä¾¡ãŒçµ‚ã‚ã£ãŸå¾Œã§ã‚‚ã€`x` ã¯ *æ°¸ä¹…ã«* `2` ã«è¨­å®šã•ã‚Œã¦ã—ã¾ã†ã‹ã‚‰ã§ã™ã€‚

ã ã‹ã‚‰ã€æ–°ã—ã„ã‚¢ã‚¤ãƒ‡ã‚£ã‚¢ãŒå¿…è¦ãªã‚“ã ã€‚ã‚¹ã‚³ãƒ¼ãƒ—`ã§ã™ã€‚ ã‚¹ã‚³ãƒ¼ãƒ—`ã¯ç’°å¢ƒã¨ã—ã¦è€ƒãˆã‚‹ã“ã¨ãŒã§ãã€ãã“ã§ã¯ `body` ãŒè©•ä¾¡ã•ã‚Œã¦ã„ã‚‹é–“ã ã‘å¤‰æ›´ãŒæŒç¶šã™ã‚‹ã€‚ã“ã®ã‚ˆã†ãªæ¦‚å¿µã‚’å°Žå…¥ã™ã‚‹ã“ã¨ãŒã§ãã‚Œã°ã€é–¢æ•°ãŒã§ãã‚‹ã“ã¨ã«ãªã‚‹ã€‚

## é–¢æ•°ã®è©•ä¾¡

ã•ã¦ã€ã“ã‚Œã‚’å‹•ä½œã•ã›ã¾ã—ã‚‡ã†ã€‚ã¾ãšã€èª°ã‹ãŒ `(clo (x) (+ x 1))` ã¨æ›¸ã„ãŸã‚‰ã€å®Ÿéš›ã«ã¯ `eval-application` ã‚’ã—ã‚ˆã†ã¨ã—ãªã„ã“ã¨ã‚’ç¢ºèªã—ã¾ã—ã‚‡ã†ã€‚ ãã®ä»£ã‚ã‚Šã€ã“ã‚Œã‚’æ–°ã—ã„ `closure` ãƒªãƒ†ãƒ©ãƒ«ã¨ã—ã¦æ‰±ã„ã¾ã™ã€‚

ãã‚Œã‚’æ¤œå‡ºã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚

```
(def closure? (partial seq-starts-with? 'clo))
```

ãã—ã¦ã€ç§ãŸã¡ã® `literal?` ã‚’æ›´æ–°ã—ã¦ãã ã•ã„ã€‚

```
(def literal?
  (some-fn string? number? boolean? char? nil? fn? closure?))
```

ä»Šã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ãŒé–¢æ•°ã‚’æ›¸ã„ãŸã ã‘ã§ã¯ã€ãã‚Œè‡ªä½“ã‚’è¿”ã—ã¦ã—ã¾ã„ã¾ã™ã€‚

```
(eval (env) (read-string "(clo (x) (+ x fav-num))"))
; => (clo (x) (+ x fav-num))
```

æ¬¡ã«ã€`ç’°å¢ƒ`ã‚’æ›´æ–°ã—ã¦ã€æ–°ã—ã„`scope`ãƒžãƒƒãƒ—ã‚’æŒã¤ã‚ˆã†ã«ã—ã¾ã™ã€‚

```
(defn env [] {:globe (HashMap. {'+ + '= = 'list list 'map map 'concat concat
                                'first first 'second second 'fav-num 41})
              :scope {}})
```

ãã—ã¦ã€`lookup-symbol` ã‚’æ›´æ–°ã—ã¦ã€ã‚¹ã‚³ãƒ¼ãƒ—å†…ã®å¤‰æ•° *ã‚‚* æŽ¢ã™ã‚ˆã†ã«ã—ã¾ã—ã‚‡ã†ã€‚

```
(defn lookup-symbol [{:keys [globe scope]} sym]
  (let [v (some (fn [m] (when (contains? m sym) [(get m sym)]))
                [globe scope])]
    (assert v (format "expected value for sym = %s" sym))
    (first v)))
```

ã‚‚ã£ã¨è¿‘ãã«ã€ã‚‚ã£ã¨è¿‘ãã«ã€‚ã•ã‚ã€å®Ÿè¡Œã•ã›ã¾ã—ã‚‡ã†ã€‚ã“ã†æ›¸ã‘ã°

```
((clo (x) (+ x fav-num)) 1)
```

ã“ã®ãƒªã‚¹ãƒˆã¯ `eval-application` ã«è¡Œãã“ã¨ã«ãªã‚‹ã§ã—ã‚‡ã†ã€‚ æ›´æ–°ã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```
(defn eval-application [env [f & args]]
  (let [f-evaled (eval env f)]
    (cond
      (fn? f-evaled) (apply f-evaled (eval-many env args))
      (closure? f-evaled) (eval-closure env f-evaled (eval-many env args)))))
```

`f-evaled` ã¯ `(clo (x) (+ x fav-num))` ã¨ãªã‚Šã¾ã™ã€‚ ã“ã‚Œã¯ `(closure? f-evaled)` ãŒçœŸã«ãªã‚‹ã“ã¨ã‚’æ„å‘³ã—ã¾ã™ã€‚

`(eval-many env args)`ã§ã™ã¹ã¦ã®å¼•æ•°ã‚’è©•ä¾¡ã™ã‚‹ã¨ `(1)` ã¨ãªã‚Šã€ã“ã‚Œã‚’ `eval-closure` ã«æ¸¡ã—ã¾ã™ã€‚ ä»¥ä¸‹ã¯ `eval-closure` ãŒã©ã®ã‚ˆã†ã«è¦‹ãˆã‚‹ã‹ã§ã™ã€‚

```
(defn eval-closure [env [_ syms body] args]
  (eval (assoc env :scope (assign-vars syms args)) body))
```

å›³ã¨åŒã˜ã‚ˆã†ã«ã—ã¾ã™ã€‚ãã—ã¦ `syms` ã¯ `(x)` ã¨ãªã‚Šã€`args` ã¯ `(1)` ã¨ãªã‚Šã¾ã™ã€‚ ãã—ã¦ã€æ–°ã—ã„ã‚¹ã‚³ãƒ¼ãƒ—ã‚’ä½œæˆã—ã¾ã™ã€‚

```
(defn assign-vars [syms args]
  (assert (= (count syms) (count args))
          (format "syms and args must match syms: %s args: %s"
                  (vec syms) (vec args)))
  (into {} (map vector syms args)))
```

ã“ã‚Œã¯ `{x 2}` ã§ã™ã€‚ ã“ã‚Œã‚’ç’°å¢ƒã«ãƒžãƒ¼ã‚¸ã—ã¦ã€`body`ã‚’è©•ä¾¡ã—ã¾ã™ã€‚

çªç„¶ã§ã™ãŒã€`(+ x fav-num)` ã¯ `43` ã«ãªã‚Šã¾ã™!

```
(eval (env) (read-string "((clo (x) (+ x fav-num)) 2)"))
; => 43
```

## ãƒ¬ã‚­ã‚·ã‚«ãƒ«ãƒ»ã‚¹ã‚³ãƒ¼ãƒ—ã‚’å°Žå…¥ã™ã‚‹

å¤šãã®è¨€èªžãŒãƒ¬ã‚­ã‚·ã‚«ãƒ«ãƒ»ã‚¹ã‚³ãƒ¼ãƒ—ã‚’ã‚µãƒãƒ¼ãƒˆã—ã¦ã„ã‚‹ã€‚ä¾‹ãˆã°ã€javascriptã§ã¯ä»¥ä¸‹ã®ã‚ˆã†ãªã“ã¨ãŒã§ãã¾ã™ã€‚

```
const makeAdder = (n) => (a) => n + a
const addTwo = makeAdder(2);
addTwo(4) 
// => 
6
```

ã“ã“ã§ã€ `makeAdder` ã¯é–¢æ•°ã‚’è¿”ã—ã¾ã™ã€‚ã“ã®é–¢æ•°ã¯ `n` ã®å€¤ã‚’ *å®šç¾©ã•ã‚Œã¦ã„ã‚‹å ´æ‰€* ã§è¨˜æ†¶ã—ã¦ã„ã¾ã—ãŸã€‚ã“ã‚Œã‚’ç§ãŸã¡ã®è¨€èªžã§å®Ÿç¾ã™ã‚‹ã«ã¯ã©ã†ã—ãŸã‚‰ã„ã„ã®ã§ã—ã‚‡ã†ã‹ï¼Ÿ

ã•ã¦ã€ã¾ãšæœ€åˆã«ã€ãƒ—ãƒ­ã‚°ãƒ©ãƒžã«ç¾åœ¨ã®ã‚¹ã‚³ãƒ¼ãƒ—ã‚’è¦‹ã›ã‚‹ã‚ˆã†ã«ã™ã‚‹ã¨ã—ã‚ˆã†ã€‚ä»¥ä¸‹ã¯ãã®ä¾‹ã§ã™ã€‚

```
((clo (x) scope) 2)
; => 
{x 2}
```

ã“ã®ç‰¹åˆ¥ãª `scope` ã‚·ãƒ³ãƒœãƒ«ã¯ã€è©•ä¾¡ã•ã‚Œã‚‹ã¨ `{x 2}` ã‚’è¿”ã—ã€ *current* ã‚¹ã‚³ãƒ¼ãƒ—ã«ãªã‚Šã¾ã™!

ã•ã¦ã€é–¢æ•°ãƒªãƒ†ãƒ©ãƒ«ã‚’æ›´æ–°ã—ã¦ã¿ã‚‹ã¨ã©ã†ã§ã—ã‚‡ã†ï¼Ÿæ¬¡ã®ã‚ˆã†ãªæ„Ÿã˜ã§ã—ã‚‡ã†ã‹ã€‚

```
(clo {n 2} (a) (+ n a))
```

ã“ã“ã§ã¯ã€`scope`ã®ãŸã‚ã®ç‰¹åˆ¥ãªå ´æ‰€ãŒã‚ã‚Šã¾ã™!  é–¢æ•°ã‚’å®šç¾©ã™ã‚‹ã¨ãã«ã€ãã®é–¢æ•°ãŒå®šç¾©ã•ã‚Œã¦ã„ã‚‹å ´æ‰€ã®ã‚¹ã‚³ãƒ¼ãƒ—ã‚’ãã“ã«ã€Œãƒãƒ³ã€ã¨ç½®ãã“ã¨ãŒã§ãã‚Œã°ã€badabing badaboom, lexical scoping ã‚’ã‚µãƒãƒ¼ãƒˆã™ã‚‹ã“ã¨ã«ãªã‚Šã¾ã™!

ã“ã“ã§ã€ `make-adder` ãŒã©ã®ã‚ˆã†ã«å‹•ä½œã™ã‚‹ã®ã‹è¦‹ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```
(def make-adder (clo nil (n) 
                  (list 'clo scope '(y) '(+ y n)))) 
(def add-two (make-adder 2))
(add-two 4)
; => 
6
```

`(make-adder 2)`ã‚’å®Ÿè¡Œã™ã‚‹ã¨ã€`(list 'clo scope '(y) '(+ y n))` ã‚’è©•ä¾¡ã™ã‚‹ã“ã¨ã«ãªã‚Šã¾ã™ã€‚ ã“ã‚Œã¯ `(clo {n 2} (y) (+ y n))` ã‚’è¿”ã™ã§ã—ã‚‡ã†!  çªç„¶ã§ã™ãŒã€ç§ãŸã¡ã¯ãƒ¬ã‚­ã‚·ã‚«ãƒ«ã‚¹ã‚³ãƒ¼ãƒ—ã‚’ã‚µãƒãƒ¼ãƒˆã—ã¦ã„ã¾ã™ã€‚

`(list 'clo scope '(y) '(+ y n)))` --- ã¨æ›¸ãã®ã¯å¤‰ã ã¨æ€ã†ã‹ã‚‚ã—ã‚Œã¾ã›ã‚“ã€‚ã§ã‚‚ã­ã€ã‚‚ã†ã™ããƒžã‚¯ãƒ­ãŒã‚µãƒãƒ¼ãƒˆã•ã‚Œã‚‹ã‹ã‚‰ã€ãã®ãŠã‹ã’ã§ã“ã®æ›¸ãæ–¹ãŒã´ã£ãŸã‚Šã«ãªã‚‹ã‚“ã ã‚ˆã€‚å¾…ã£ã¦ã¦ã­ ðŸ™‚.

## Implementing Lexical Scope

Okay. that's a lot of explaining, but the changes will be simple. First we need to make sure that if we get a `scope` symbol, we return the current scope:

```
(defn lookup-symbol [{:keys [globe scope]} sym]
  (let [v (some (fn [m] (when (contains? m sym) [(get m sym)]))
                [{'scope scope} globe scope])]
    (assert v (format "expected value for sym = %s" sym))
    (first v)))
```

And now all we need to do, is to update `eval-closure`:

```
(defn eval-closure [env [_ scope syms body] args]
  (eval (assoc env :scope (merge scope (assign-vars syms args))) body))
```

Here we take the `scope` *from* the closure itself! With that, lexical scoping works!

```
(((clo nil (x)
        (list 'clo scope '(y) '(+ y x))) 2) 4)
; => 
6
```

## Introducing Macros

Now, we have all we need to implement macros! Let's think about what macros really are.

Imagine writing the function `unless`:

```
(def unless (clo nil (test v)
              (if (not test) v)))
```

Will it work? If we ran `(unless (= fav-num 41) 'yes)`, it *would* return the symbol `yes`.

But what if we ran this?

```
(unless (= fav-num 41) (throw-error))
```

Since `unless` is a function, we would evaluate each argument first right? In that case, we'd evaluate `(= fav-num 41)`, which would be `true`.  But, we'd *also* evaluate `(throw-error)`.  That would break our program. This defeats the whole purpose of `unless` , as `(throw-error)` was supposed to run *only* when `test` was false.



![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc3Mi1hNTlmNGJkOC1mNDc1LTQxMGMtODFmYi02YWI2NjlhNDY1OTEucG5n){style="max-width:100%"}


Now, imagine we wrote the *macro* unless:

```
(def unless (mac (clo nil (test v)
                    (list 'if (list 'not test) v))))
```

If we ran `(unless (= fav-num 41) (throw-error))`, here's what would happen:

The value of `test` would not be `true`, it would actually be the list `(= fav-num 41)`.  Similarly, we wouldn't evaluate `(throw-error)` .  `v` would just be the actual list `(throw-error)`.

*The arguments to a macro are not evaluated.* When the macro runs, it returns *code.* The result of

```
(list 'if (list 'not test) v))
```

would become

```
(if (not (= fav-num 41)) (throw-error))
```

And when we evaluated *that,* things would work as expected!  `(throw-error)` would never evaluate.

This is the key difference between functions and macros. Functions take *evaluated values* as arguments and return evaluated values. Macros receive *unevaluated* arguments, return *code, and that code is evaluated.*


![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc3OS1mMDMxNWQyNS1lZjZlLTQzYjktOTBiNy1jMmYyMDE2YTkyOGIucG5n){style="max-width:100%"}


An important piece to note is that we eval *twice.* We give the macro the unevaluated arguments, which returns new code, and we once again *evaluate* that.

So, how can we support this?

## Evaluating Macros

So much explanation, for such little code. Are you ready? Let's add macros.

First, let's use this new structure for a macro: a macro is a list that begins with `mac`, and has closure inside:

```
(mac (clo nil (...))
```

We can detect macro, and mark it as a literal:

```
(def macro? (partial seq-starts-with? 'mac))
    
(def literal?
  (some-fn string? number? boolean? char? nil? fn? closure? macro?))
```

Next, we'll want to update `eval-application`:

```
(defn eval-application [env [f & args]]
  (let [f-evaled (eval env f)]
    (cond
      (fn? f-evaled) (apply f-evaled (eval-many env args))
      (closure? f-evaled) (eval-closure env f-evaled (eval-many env args))
      (macro? f-evaled) (eval-macro env f-evaled args))))
```

Before we always ran `(eval-many env args)`.  But this time, if it's a macro, we pass in `args` directly! That's the "code" itself ðŸ™‚.

And now for `eval-macro`:

```
(defn eval-macro [env [_ clo] args]
  (eval env
        (eval env (concat [clo] (map (partial list 'quote) args)))))
```

Oh my god. 3 lines!! We do exactly as we said in our diagram:



![image](/api/image/aHR0cHM6Ly91c2VyLWltYWdlcy5naXRodWJ1c2VyY29udGVudC5jb20vOTg0NTc0LzE1MDY1NTc5MC02MWFhOTQ5OC05NDgzLTQ4ODYtOGRiMC1lYmM3NzE1OTM2MzkucG5n){style="max-width:100%"}


We take the "closure" out of our macro, and *run* it with *unevaluated* args. We can do that just by wrapping each `arg` in a quote: `(map (partial list 'quote) args)`.

Once we have the resulting code, we *evaluate* that again, and boom, we have macros.

```
(def unless (mac (clo nil (test v)
                    (list 'if (list 'not test) v))))
; works!
(unless (= fav-num 41) (throw-error))
```

# Your own Macros

Okay, we have a Lisp that supports macros. Let's go ahead and write some of our own!

## defmacro

Let's get meta off the bat. Notice what we do when we define a macro:

```
(def unless (mac (clo nil (test v)
                      (list 'if (list 'not test) v))))
```

This whole `(mac (clo nil â€¦))` business is a bit unnecessary. Let's just write macro that does this for us!

```
(def defmacro
      (mac (clo nil (n p e)
                (list 'def n
                      (list 'mac (list 'clo nil p e))))))
```

This generates the code `(def n (mac (clo nil â€¦)))`.  Now we could write:

```
(defmacro unless (test v) 
  (list 'if (list 'not test) v))
```

Cool!

## fn

Okay, remember how we wrote our function for lexical scoping?

```
(def make-adder (clo nil (n)
                      (list 'clo scope '(y) '(+ y n))))
```

Let's have a macro write this for us:

```
(defmacro fn (args body)
              (list 'list ''clo 'scope
                    (list 'quote args)
                    (list 'quote body)))
```

Here's what happens if we wrote:

```
(def make-adder (clo nil (n)
                    (fn (y) (+ y n))))

(def add-two (make-adder 2))
```

When the macro expansion for `fn` runs, the `args` would would be `(y)` and `body` `(+ y n)`.  So

```
(list 'list ''clo 'scope
      (list 'quote args)
      (list 'quote body))
```

would expand to

```
(list 'clo scope '(y) '(+ y n))
```

and that's *exactly* what we wrote by hand! Bam, now we can use `fn` instead of this whole `'clo scope` business.

## defn

Now if we wanted to define functions, we could write:

```
(def add-fav-num (fn (x) (+ x fav-num)))
```

But we can make it tighter. Let's add a `defn` macro, so we could write:

```
(defn add-fav-num (x) (+ x fav-num))
```

Easy peasy:

```
(defmacro defn (n args body)
  (list 'def n (list 'fn args body)))
```

## let

One more cool macro. Right now, there's no real way to define temporary variables. Something like:

```
(let ((x 1) (y 2)) 
  (+ x y))
; => 
3
```

How could we support it? Well, what if we *rewrote* `(let â€¦)` to:

```
((fn (x y) (+ x y)) 1 2)
```

We could just use the argument list of a function to create these variables! Perfecto. Here's the macro:

```
(defmacro let (pairs body)
  (concat (list (list 'fn (map first pairs) body))
                (map second pairs)))
```

# Fin

What a journey. 80 lines, 4000 words, and hopefully a fun time ðŸ™‚. You now have a Lisp with macros, and you've written some cool ones yourself.  Here's the [full source](https://github.com/stopachka/llisp/blob/main/src/llisp/core.clj){style="text-decoration:underline" target="_blank"} if you'd like to follow along from there.

**Credits**

The idea to represent macros and closures like this, came from PG's [Bel](http://www.paulgraham.com/bel.html){style="text-decoration:underline" target="_blank"}.

*Thanks to Mark Shlick, Daniel Woelfel for reviewing drafts of this essay.*


------------------------------------------------------------------------

#### *Thoughts? Reach out to me via twitter or email : )*







